include_guard(GLOBAL)

macro(devkitpro_message level msg)
    if(${level} STREQUAL "CHECK_PASS" OR ${level} STREQUAL "CHECK_FAIL")
        message(${level} "${msg}")
    else()
        message(${level} "[devkitPro] ${msg}")
    endif()
endmacro()

function(devkitpro_find_file variable file_path)
    cmake_path(GET file_path STEM file_stem)
    devkitpro_message(CHECK_START "Finding ${file_stem}")
    if(DEFINED ${variable})
        if(EXISTS ${${variable}})
            devkitpro_message(CHECK_PASS "Using pre-set location: ${${variable}}")
        else()
            devkitpro_message(CHECK_FAIL "Invalid pre-set location: ${${variable}}")
            message(FATAL_ERROR)
        endif()
    else()
        if(NOT DEVKITPRO)
            devkitpro_message(CHECK_FAIL "DEVKITPRO must be defined")
            message(FATAL_ERROR)
        endif()
        cmake_path(ABSOLUTE_PATH file_path BASE_DIRECTORY ${DEVKITPRO})
        if(EXISTS ${file_path})
            set(${variable} "${file_path}" PARENT_SCOPE)
            devkitpro_message(CHECK_PASS "Found ${file_path}")
        else()
            devkitpro_message(CHECK_FAIL "Not at expected location ${file_path}")
            message(FATAL_ERROR)
        endif()
    endif()
endfunction(devkitpro_find_file)